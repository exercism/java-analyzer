plugins {
    id "java"
    id "application"
    id "jacoco"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "ru.vyarus.mkdocs-build" version '4.0.1'
}

group = "org.exercism"
version = "1.0-SNAPSHOT"
mainClassName = "analyzer.AnalyzerCli"

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.code.gson:gson:2.11.0"
    implementation "com.github.javaparser:javaparser-core:3.26.3"

    testImplementation platform("org.junit:junit-bom:5.11.4")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core:3.27.0"
    testImplementation "com.approvaltests:approvaltests:24.15.0"
}

shadowJar {
    mergeServiceFiles()
    archiveFileName.set("java-analyzer.jar")
}

artifacts {
    archives shadowJar
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = "full"
        showStandardStreams = true
        events = ["passed", "failed", "skipped"]
    }

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

mkdocs {
    sourcesDir = "src/doc"
    buildDir = file("build/docs")
    publish.docPath = null
}

javadoc {
    mustRunAfter(mkdocsBuild)
    destinationDir = file("build/docs/api")
}
