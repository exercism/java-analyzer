plugins {
    id "java"
    id "application"
    id "jacoco"
    id 'com.gradleup.shadow' version '9.3.1'
    id "ru.vyarus.mkdocs-build" version '4.0.1'
}

group = "org.exercism"
version = "1.0-SNAPSHOT"

application {
    mainClass = "analyzer.AnalyzerCli"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.code.gson:gson:2.13.2"
    implementation "com.github.javaparser:javaparser-core:3.28.0"

    testImplementation platform("org.junit:junit-bom:5.11.4")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core:3.27.4"
    testImplementation "com.approvaltests:approvaltests:26.6.1"

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

shadowJar {
    mergeServiceFiles()
    archiveFileName.set("java-analyzer.jar")
}

artifacts {
    archives shadowJar
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = "full"
        showStandardStreams = true
        events = ["passed", "failed", "skipped"]
    }

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

mkdocs {
    sourcesDir = "src/doc"
    buildDir = file("build/docs")
    publish.docPath = null
}

javadoc {
    mustRunAfter(mkdocsBuild)
    destinationDir = file("build/docs/api")
}
