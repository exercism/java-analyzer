plugins {
    id "java"
    id "application"
    id "jacoco"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "ru.vyarus.mkdocs-build" version '3.0.0'
}

group = "org.exercism"
version = "1.0-SNAPSHOT"
mainClassName = "analyzer.AnalyzerCli"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.json:json:20231013"
    implementation "com.github.javaparser:javaparser-core:3.25.7"

    testImplementation platform("org.junit:junit-bom:5.10.1")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core:3.25.1"
}

shadowJar {
    mergeServiceFiles()
    archiveFileName.set("java-analyzer.jar")
}

artifacts {
    archives shadowJar
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = "full"
        showStandardStreams = true
        events = ["passed", "failed", "skipped"]
    }

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

mkdocs {
    sourcesDir = "src/doc"
    buildDir = file("build/docs")
    publish.docPath = null
}

javadoc {
    mustRunAfter(mkdocsBuild)
    destinationDir = file("build/docs/api")
}
